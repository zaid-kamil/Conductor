apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = 'com.bluelinelabs'
version = rootProject.ext.versionName

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    exclude '**/R.java'
    failOnError = false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

if (project.hasProperty('pom_name')) {
    install {
        repositories.mavenInstaller {
            pom.project {
                name pom_name
                description pom_description
                url pom_url
                packaging pom_packaging
                groupId 'com.bluelinelabs'
                artifactId project.hasProperty('artifactId') ? project.ext.artifactId : ''

                organization {
                    name 'BlueLine Labs'
                    url 'http://bluelinelabs.com'
                }
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                scm {
                    url pom_url
                    connection pom_git_connection
                    developerConnection pom_git_connection
                }
                developers {
                    developer {
                        id 'erickuck'
                        name 'Eric Kuck'
                    }
                }
            }
        }
    }

    bintray {
        user = project.hasProperty('bintray_username') ? bintray_username : ''
        key = project.hasProperty('bintray_api_key') ? bintray_api_key : ''
        configurations = ['archives']

        dryRun = false
        publish = false

        pkg {
            repo = 'bluelinelabs'
            userOrg = 'bluelinelabs'
            name = pom_name
            desc = pom_description
            websiteUrl = pom_url
            issueTrackerUrl = pom_issue_tracker_url
            vcsUrl = pom_url
            licenses = ['Apache-2.0']
            labels = pom_labels

            version {
                name = project.version
                gpg {
                    sign = true
                    passphrase = project.hasProperty('bintray_gpg_passphrase') ? bintray_gpg_passphrase : ''
                }
                mavenCentralSync {
                    sync = false
                    user = project.hasProperty('maven_central_username') ? maven_central_username : ''
                    password = project.hasProperty('maven_central_password') ? maven_central_password : ''
                    close = '1'
                }
            }
        }
    }
}
